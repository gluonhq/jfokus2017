buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.3.2'
    }

}

plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

license {
    header = file('LICENSE')
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
    excludes(['eu/hansolo/**/*.java', '**/*.json'])
}


apply plugin: 'org.javafxports.jfxmobile'
apply from: 'theme.gradle'

themes {
    otn
    javaone
}

repositories {
    jcenter()
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/releases'
    }
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/snapshots'
        credentials {
            username gluonNexusUsername
            password gluonNexusPassword
        }
    }
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/gluon-oss-daily'
        credentials {
            username gluonNexusUsername
            password gluonNexusPassword
        }
    }
    maven {
        url 'http://nexus.gluonhq.com/nexus/content/repositories/gluon-private-releases'
        credentials {
            username gluonNexusUsername
            password gluonNexusPassword
        }
    }
}

mainClassName = 'com.gluonhq.otn.OTNApplication'
preloaderClassName = 'com.gluonhq.otn.OTNPreloader'

// For desktop (for mobile check java.custom.properties)
applicationDefaultJvmArgs = [
        "-Djavafx.pulseLogger=false",
        "-Denable.logging=false",
        "-Dfile.encoding=UTF-8"
]

dependencies {
    compile 'com.gluonhq:glisten-afterburner:1.2.0-SNAPSHOT'
    compile 'com.gluonhq:charm-cloudlink-client:4.4.0-SNAPSHOT'

    compile 'com.gluonhq:connect:1.4.0'
    compile 'com.google.zxing:core:3.2.1'
    compile 'com.gluonhq:maps:1.0.1'
    compile 'com.gluonhq:glisten-labs:1.0.0-b6'
}

configurations.all {
    resolutionStrategy {
        cacheChangingModulesFor 0, 'seconds'
    }
}

jfxmobile {
    javafxportsVersion = '8.60.d161202'
    downConfig {
        version '3.2.0'
        plugins 'browser', 'cache', 'connectivity', 'dialer', 'display', 'lifecycle', 'local-notifications', 'settings', 'statusbar', 'storage'
    }
    android {
        manifest = 'src/android/AndroidManifest.xml'
        signingConfig {
            storeFile project.hasProperty('GLUON_APK_SIGN_KEYSTORE') ? file(GLUON_APK_SIGN_KEYSTORE) : null
            storePassword project.hasProperty('GLUON_APK_SIGN_STOREPASSWORD') ? GLUON_APK_SIGN_STOREPASSWORD : ''
            keyAlias project.hasProperty('GLUON_APK_SIGN_KEYALIAS') ? GLUON_APK_SIGN_KEYALIAS : ''
            keyPassword project.hasProperty('GLUON_APK_SIGN_KEYPASSWORD') ? GLUON_APK_SIGN_KEYPASSWORD : ''
        }
    }
    ios {
        infoPList = file('src/ios/Default-Info.plist')
        forceLinkClasses = [
                'com.gluonhq.otn.**.*',
                'com.gluonhq.**.*',
                'javax.xml.stream.*.',
                'io.datafx.**.*',
                'javax.annotations.**.*',
                'javax.inject.**.*',
                'javax.json.**.*',
                'org.glassfish.json.**.*'
        ]
    }
}
